#! winxed

// parsing example

//*********************************************
//          Character test functions
//*********************************************

function isspace(string c)
{
    return c == ' ' || c == "\n" || c == "\t";
}

function isidentstart(string c)
{
    return indexof(
        'abcdefghijklmnopqrstuvwxyz' +
        'ABCDEFGHIJKLMNOPQRSTUVWXYZ' + '_',
        c) > -1;
}

function isident(string c)
{
    return indexof(
        'abcdefghijklmnopqrstuvwxyz' +
        'ABCDEFGHIJKLMNOPQRSTUVWXYZ' + '_' +
        '0123456789',
        c) > -1;
}

//*********************************************
//                Token
//*********************************************

class Token
{
    var type;
    var str;
    var line;
    function get_bool[vtable]()
    {
        return self.type == -1;
    }
    function iseof()
    {
        return self.type == -1;
    }
    function iscomment() { return self.type == 0; }
    function iskeyword(string name)
    {
        return self.type == 4 && self.str == name;
    }
    function isop(string name)
    {
        return self.type == 5 && self.str == name;
    }

    function get_string[vtable]()
    {
        string str= self.str;
        switch (self.type)
        {
        case 1:
            str= '"' + str + '"';
            break;
        case 2:
            str= "'" + str + "'";
            break;
        }
        return str;
    }
}

function newToken(int type, string s, int line)
{
    var t= new Token;
    t.type= type; t.str= s; t.line= line;
    return t;
}

//*********************************************
//       Tokenizer auxiliar functions
//*********************************************

function getquoted(tk, string start)
{
    int line= tk.line;
    string s;
    string c= tk.getchar();
    while (c != '"')
    {
        if (c == '' || c == "\n")
            throw Error('Unterminated string in ' + line);
        s+= c;
        c= tk.getchar();
    }
    return newToken(2, s, line);
}

function getsinglequoted(tk, string start)
{
    int line= tk.line;
    string s;
    string c= tk.getchar();
    while (c != "'")
    {
        if (c == '' || c == "\n")
            throw Error('Unterminated string in ' + line);
        s+= c;
        c= tk.getchar();
    }
    return newToken(3, s, line);
}

function getident(tk, string start)
{
    int line= tk.line;
    string s= start;
    string c= tk.getchar();
    while (isident(c))
    {
        s+= c;
        c= tk.getchar();
    }
    tk.ungetchar(c);
    return newToken(4, s, line);
}

function getlinecomment(tk, string start)
{
    int line= tk.line;
    string s= start;
    string c= tk.getchar();
    while (c != '' && c != "\n")
    {
        s+= c;
        c= tk.getchar();
    }
    return newToken(0, s, line);
}

function getcomment(tk, string start)
{
    int line= tk.line;
    string s= start;
    string c= tk.getchar();
next:
    while (c != '' && c != '*')
    { s+= c; c= tk.getchar(); }
    if (c == '')
        throw Error("Unclosed comment");
    c= tk.getchar();
    if (c == '')
        throw Error("Unclosed comment");
    if (c != '/')
    { s+= '*' + c; goto next; }
    s+= '*/';
    return newToken(0, s, line);
}

function getop(tk, string start)
{
    int line= tk.line;
    string s= start;
    var t= new Token;
    return newToken(5, s, line);
}

//*********************************************
//              Tokenizer
//*********************************************

class Tokenizer
{
    var h;
    var pending;
    var select;
    var line;
    function init(handle)
    {
        self.h= handle;
        var pending= new 'String';
        self.pending= pending;
        self.line= 1;
        var select = {
            '"': { '': getquoted },
            "'": { '': getsinglequoted },
            '=': { '': getop, '=': getop },
            '+': { '': getop, '+': getop, '=': getop },
            '-': { '': getop, '-': getop, '=': getop },
            '|': { '': getop, '|': getop },
            '&': { '': getop, '&': getop },
            '<': { '': getop, '<': getop, '=': getop },
            '>': { '': getop, '>': getop, '=': getop },
            '!': { '': getop, '=': getop },
            '/': { '': getop, '/': getlinecomment, '*': getcomment },
            '#': { '': getlinecomment }
        };
        self.select= select;
    }
    function getchar()
    {
        string pending= self.pending;
        if (pending != '')
        {
            self.pending= '';
            return pending;
        }
        var h= self.h;
        string c= h.read(1);
        if (c == "\n")
        {
                int l=self.line;
                ++l;
                self.line= l;
        }
        return c;
    }
    function ungetchar(string c)
    {
        self.pending= c;
    }
    function get_token()
    {
        string c= self.getchar();
        while (isspace(c))
                c= self.getchar();
        if (c == '')
        {
            var t= new Token;
            t.type= -1;
            return t;
        }
        if (isidentstart(c)) return getident(self, c);
        var select = self.select;
        var current= select[c];
        if (current)
        {
            string c2= self.getchar();
            var current2= current[c2];
            if (current2)
            {
                c+= c2;
                return current2(self, c);
            }
            else
            {
                self.ungetchar(c2);
                current2= current[''];
                if (current2)
                    return current2(self, c);
            }
        }
        else
        {
            int line= self.line;
            return newToken(5, c, line);
        }
    }
    function get(int withcomments [optional])
    {
        var t= self.get_token();
        while (!t.iseof() && ! withcomments && t.iscomment() )
           t= self.get_token();
        return t;
    }
}

//*********************************************
//            Miniwinxed
//*********************************************

// This is a first step towards a possible bootstraped version.

class FunctionStatement
{
    var name;
    var modifiers;

    function parse(tk)
    {
        var t= tk.get();
        say('name: ', t);
        self.name= t;
        t= tk.get();
        if (t.isop('['))
        {
            var modifiers= [];
            t= tk.get();
            modifiers.push(t);
            for (t= tk.get(); t.isop(','); )
            {
                t= tk.get();
                modifiers.push(t);
            }
            if (!t.isop(']'))
                throw Error("Expected ',' or ']'");
            t= tk.get();
            self.modifiers= modifiers;
        }
        if (! t.isop('('))
            throw Error("Expected '('");
        t= tk.get();
        while (! t.isop(')'))
            t= tk.get();
        t= tk.get();
        if (!t.isop('{'))
            throw Error("Expected '{'");
        int blocklevel= 1;
        while (blocklevel > 0)
        {
            t= tk.get();
            if (t.isop('{'))
                ++blocklevel;
            else if (t.isop('}'))
                --blocklevel;
        }
    }

    function showinfo()
    {
            print('function ', self.name);
            var modifiers= self.modifiers;
            if (modifiers)
            {
                print(' [');
                var m;
                string sep= '';
                for (m in modifiers)
                {
                    print(sep);
                    sep= ', ';
                    print(m);
                }
                print(']');
            }
            say(' ()');
    }
}

class ClassStatement
{
    var name;
    var functions;

    function init [vtable] ()
    {
        var functions= [];
        self.functions = functions;
    }

    function parse(tk)
    {
        var t= tk.get();
        say('name: ', t);
        self.name= t;
        t= tk.get();
        if (!t.isop('{'))
            throw Error("Expected '{'");
        t= tk.get();
        while (!t.isop('}'))
        {
            if (t.iskeyword('function'))
            {
                say('Got a member function!');
                var f= new FunctionStatement;
                f.parse(tk);
                var functions= self.functions;
                functions.push(f);
            }
            t= tk.get();
        }
    }

    function showinfo()
    {
        say('class ', self.name, ' {');
        var f;
        for (f in self.functions)
            f.showinfo();
        say('}');
    }
}

class MiniWinxed
{
    var balance1;
    var balance2;
    var balance3;
    var functions;
    var classes;
    function init [vtable] ()
    {
        self.balance1= 0;
        self.balance2= 0;
        self.balance3= 0;
        var functions= [];
        self.functions = functions;
        var classes= [];
        self.classes= classes;
    }
    function parse(tk)
    {
        var t= tk.get();
        int balance1= 0;
        int balance2= 0;
        int balance3= 0;
        while (! t)
        {
            if (t.iskeyword('function'))
            {
                say('Got a function!');
                var f= new FunctionStatement;
                f.parse(tk);
                var functions= self.functions;
                functions.push(f);
            }
            else if (t.iskeyword('class'))
            {
                say('Got a class!');
                var c= new ClassStatement;
                c.parse(tk);
                var classes= self.classes;
                classes.push(c);
            }
            else if (t.type == 5)
            {
                string s= t;
                switch(s)
                {
                case '(': ++balance1; break;
                case ')': --balance1; break;
                case '{': ++balance2; break;
                case '}': --balance2; break;
                case '[': ++balance3; break;
                case ']': --balance3; break;
                }
            }
            t= tk.get();
        }
        self.balance1= balance1;
        self.balance2= balance2;
        self.balance3= balance3;
    }
    function report()
    {
        var f;
        for (f in self.functions)
            f.showinfo();
        var c;
        for (c in self.classes)
            c.showinfo();

        if (self.balance1) say('Unbalanced ( ): ', self.balance1);
        if (self.balance2) say('Unbalanced [ ]: ', self.balance2);
        if (self.balance3) say('Unbalanced { }: ', self.balance3);
    }
}

function miniwinxed_parser(tk)
{
        var winxed= new MiniWinxed;
        winxed.parse(tk);
        winxed.report();
}

//*********************************************
//            Main functions
//*********************************************

function parser(argv)
{
    // Simple example, get tokens and prints them.

    using extern Getopt.Obj;
    var getopts = new 'Getopt::Obj';
    getopts.notOptStop(1);
    getopts.push_string('withcomments');
    getopts.push_string('miniwinxed');

    argv.shift();
    var opts = getopts.get_options(argv);
    int withcomments= opts['withcomments'];
    int miniwinxed = opts['miniwinxed'];

    int argc= argv;
    if (argc == 0)
        throw Error('No file');
    var file= new 'FileHandle';
    file.open(argv[0]); // Must throw on error

    var t= new Tokenizer;
    t.init(file);

    if (miniwinxed)
        miniwinxed_parser(t);
    else
    {
        var c;
        int line= 0;
        c= t.get(withcomments);
        while (! c)
        {
            int newline= c.line;
            if (newline != line)
            {
                line= newline;
                say(); print(line, ': ');
            }
            print(" [", c, "]");
            c= t.get(withcomments);
        }
        say('');
    }

    file.close();
}

function main(argv)
{
    try parser(argv);
    /**/
    catch (e)
    {
        string msg= e['message'];
        say('Error: ', msg);
    }
    /**/
}

// End
