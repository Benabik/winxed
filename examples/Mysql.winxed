#! winxed

// Test Mysql usage via NCI

// Severity and type of the exceptions explicitly thrown.
const int ERROR = 2;
const int TYPE= 99;

// Note:
// null results from dlfuncs are checked in a convoluted way
// to be able to work with buggy versions of parrot.

namespace WinxedMysql
{

function getlib__private()
{
    string libs[] = [
        'libmysqlclient',
        'libmysqlclient.so.16',
        'libmysqlclient.so.15'
    ];
    var l;
    for (string lib in libs) {
        l= loadlib(lib);
        if (l) break;
    }
    return l;
}

function getlib()
{
    var l= getlib__private();
    if (! l)
        throw Error('Cannot load Mysql lib', ERROR, TYPE);
    for (;;)
        yield l;
}

class Row
{
    var mrow;
    var nfields;
    function init(var myrow, int n)
    {
        self.mrow= myrow;
        self.nfields= n;
    }
    function get(int i)
    {
        var myrow= self.mrow;
        string s= myrow[i];
        return s;
    }
}

class Result
{
    var mysql;
    var myresult;
    var nfields;
    var desc;
    function init(var my, var r)
    {
        using WinxedMysql.getlib;
        self.mysql= my;
        self.myresult= r;
        var f= dlfunc(getlib(), 'mysql_field_count', 'ip');
        int count= f(my);
        self.nfields= count;
        int desc [3 * count];
        int i;
        // Store the fields description for use in fetchs.
        for (i= 0; i < count; ++i) {
            desc[i * 3]    = -70;
            desc[i * 3 + 1]= 1;
            desc[i * 3 + 2]= 0;
        }
        self.desc= desc;
    }
    function field_count()
    {
        return self.nfields;
    }
    function fetch_row()
    {
        using WinxedMysql.getlib;
        var f= dlfunc(getlib(), 'mysql_fetch_row', 'pp');
        var frow= f(self.myresult);
        var row;

        if(frow == null) return row;
        var none = new 'UnManagedStruct';
        if (frow == none) return row;

        var desc= self.desc;
        frow =: desc;
        row= new WinxedMysql.Row();
        row.init(frow, self.nfields);
        return row;
    }
    function close()
    {
        if (self.myresult != null)
        {
            using WinxedMysql.getlib;
            var f= dlfunc(getlib(), 'mysql_free_result', 'vp');
            f(self.myresult);
            self.myresult= null;
        }
    }
}

class Connection
{
    var mysql;
    function init [vtable]()
    {
        using WinxedMysql.getlib;
        var minit= dlfunc(getlib(), 'mysql_init', 'pt');
        string nothing;
        var my= minit(nothing);
        self.mysql= my;
    }
    function close()
    {
        using WinxedMysql.getlib;
        var mclose= dlfunc(getlib(), 'mysql_close', 'vp');
        mclose(self.mysql);
        var n= new 'Undef';
        self.mysql= n;
    }
    function get_client_info()
    {
        using WinxedMysql.getlib;
        var f= dlfunc(getlib(), 'mysql_get_client_info', 't');
        string s= f();
        return s;
    }
    function error()
    {
        using WinxedMysql.getlib;
        var f= dlfunc(getlib(), 'mysql_error', 'tp');
        string s= f(self.mysql);
        return s;
    }
    function connect(string host, string user, string pass, string database)
    {
        using WinxedMysql.getlib;
        var f= dlfunc(getlib(), 'mysql_real_connect', 'ppttttitl');
        string snull;
        var p= f(self.mysql, host, user, pass, database, 0, snull, 0);
        if (p == null) throw Error(self.error(), ERROR, TYPE);
        var none = new 'UnManagedStruct';
        if (p == none) throw Error(self.error(), ERROR, TYPE);
        return p;
    }
    function query(string stmt)
    {
        using WinxedMysql.getlib;
        var f= dlfunc(getlib(), 'mysql_query', 'ipt');
        int q= f(self.mysql, stmt);
        if (q != 0) throw Error(self.error(), ERROR, TYPE);
    }
    function use_result()
    {
        using WinxedMysql.getlib;
        var f= dlfunc(getlib(), 'mysql_use_result', 'pp');
        var r= f(self.mysql);
        if (r == null) throw Error(self.error(), ERROR, TYPE);
        var none = new 'UnManagedStruct';
        if (r == none) throw Error(self.error(), ERROR, TYPE);
        var result= new WinxedMysql.Result();
        result.init(self.mysql, r);
        return result;
    }
}

} // namespace WinxedMysql

function main(argv)
{
    using extern Getopt.Obj;
    var getopts = new Getopt.Obj();
    getopts.notOptStop(1);
    getopts.push_string('host|h=s');
    getopts.push_string('user|u=s');
    getopts.push_string('password|p:s');
    getopts.push_string('database|D=s');
    getopts.push_string('config|D=s');
    argv.shift();
    var opts = getopts.get_options(argv);

    string host;
    string user;
    string pass;
    string database;

    string config = opts['config'];
    if (config != null && config != '') {
        // Get config from file in json format.
        try {
            var json = load_language('data_json');
            var code = json.compile(open(config).readall());
            var data = code();
            if (data['host'] != null)
                host = data['host'];
            if (data['user'] != null)
                user = data['user'];
            if (data['password'] != null)
                pass = data['password'];
            if (data['database'] != null)
                database = data['database'];
        }
        catch(e) {
            cry('Error reading config file: ', e['message']);
            return;
        }
    }

    host = opts['host'];
    if (opts['user'] != null)
        user = opts['user'];
    if (opts['password'] != null)
        pass= opts['password'];
    if (opts['database'] != null)
        database = opts['database'];

    if (host == null || host == '') host = 'localhost';
    if (user == null || user == '') user = 'parrot';

    // Need a var here, keyed string access returns empty
    // string instead of null in current parrot versions.
    if (pass == null)
        pass = 'baDworD';
    else if (pass == '')
        die("Sorry, asking for password not implemented");

    if (database == null || database == '') database = 'parrot';

    var mysql= new WinxedMysql.Connection();
    say('Mysql version: ', mysql.get_client_info());
    var result;

    try [min_severity(ERROR),max_severity(ERROR),handle_types(TYPE)] {
        mysql.connect(host, user, pass, database);
        string q= 'select * from hello;';
        if (argv[0])
            q= argv[0];
        say("Query: '", q, "'");
        mysql.query(q);
        result= mysql.use_result();
        int fields= result.field_count();

        int nrows= 0;
        var row= result.fetch_row();
        while (row != null) {
            ++nrows;
            print(nrows, ': ');
            int i;
            for (i= 0; i < fields; ++i) {
                if (i > 0) print(", ");
                var s= row.get(i);
                if (s == null) print('(null)');
                else print("'", s, "'");
            }
            say();
            row= result.fetch_row();
        }
        result.close();
        say('Total rows: ', nrows);
    }
    catch (e) {
        say("\tERROR: ", e['message']);
        if (result != null) result.close();
        mysql.close();
        return;
    }
    mysql.close();
}
