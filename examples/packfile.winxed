#! winxed

# Show the content of a pbc file using the Packfile PMCs.
# Work in progress.

// Segment types (from packfile_segments.pasm)
const int PF_DIR_SEG         = 0;
const int PF_UNKNOWN_SEG     = 1;
const int PF_FIXUP_SEG       = 2;
const int PF_CONST_SEG       = 3;
const int PF_BYTEC_SEG       = 4;
const int PF_DEBUG_SEG       = 5;
const int PF_ANNOTATIONS_SEG = 6;

// Constant types (from packfile_constants.pasm)
const int PFC_NONE =   0x00;
const int PFC_NUMBER = 0x6E;
const int PFC_STRING = 0x73;
const int PFC_PMC =    0x70;
const int PFC_KEY =    0x6B;

// Opcode arg types (from include/parrot/enums.h and include/parrot/op.h)
const int PARROT_ARG_INTVAL    = 0x0000;
const int PARROT_ARG_STRING    = 0x0001;
const int PARROT_ARG_PMC       = 0x0002;
const int PARROT_ARG_FLOATVAL  = 0x0003;
const int PARROT_ARG_TYPE_MASK = 0x000F;
const int PARROT_ARG_CONSTANT  = 0x0010;
const int PARROT_ARG_KEYED     = 0x0020;

function dump_constants(var seg)
{
    int n = elements(seg);
    float numval;
    say("Constant table:\n\tLength: ", n);
    for (int i= 0; i < n; ++i) {
        int type = seg.get_type(i);
        string typename;
        string value = '';
        switch (type) {
        case PFC_KEY:
            typename = 'KEY';
            break;
        case PFC_NUMBER:
            typename = 'NUMBER';
            numval = seg[i];
            value = numval;
            break;
        case PFC_PMC:
            typename = 'PMC';
            break;
        case PFC_STRING:
            typename = 'STRING';
            value = seg[i];
            value = '"' + value + '"';
            break;
        default:
            typename = 'INVALID CONSTANT TYPE';
        }
        say("\t", i, ': ', typename, '(', type, '): ', value);
    }
}

function dump_fixup(var seg, var const_table)
{
    int n = elements(seg);
    float numval;
    say("Fixup table:\n\tLength: ", n);
    for (int i= 0; i < n; ++i) {
        var entry = seg[i];
        int type = entry.get_type();
        say("\tLabel: '", entry, "' type: ", type);

        int index = entry;
        var pmcsub = const_table[index];
        int offset = pmcsub.start_offs();
        string fullname;
        var ns = pmcsub.get_namespace();
        if (ns != null) {
            var nsname = ns.get_name();
            fullname = nsname;
            fullname += ' ';
        }
        fullname += pmcsub;
        say("\t\tSub: ", fullname);

        string subid = pmcsub.get_subid();
        if (subid != null && subid != '')
            say("\t\tID: ", subid);
        say("\t\tOffset: ", offset);
    }
}

function dump_annotations(var seg)
{
    int n = elements(seg);
    float numval;
    say("Annotations:\n\tLength: ", n);
    for (int i= 0; i < n; ++i) {
        var annot = seg[i];
        int offset = annot.get_offset();
        string name = annot.get_name();
        print("\tOffset: ", offset, ' type: "', name, '" value: ');
        switch (name) {
        case 'file':
            string filename = annot;
            print("'", filename, "'");
            break;
        case 'line':
            int line = annot;
            print(line);
            break;
        }
        say();
    }
}

function showarg(int type, int value, var const_table)
{
    const string regtypename = 'ISPN';
    if (type & PARROT_ARG_CONSTANT) {
        switch(type & PARROT_ARG_TYPE_MASK) {
        case PARROT_ARG_INTVAL:
            print(value);
	    break;
        case PARROT_ARG_STRING:
            string sval = const_table[value];
            print("'", sval, "'");
	    break;
        case PARROT_ARG_PMC:
	    if (type & PARROT_ARG_KEYED)
                print("KC", value);
	    else
                print("PC", value);
	    break;
        case PARROT_ARG_FLOATVAL:
            float fval = const_table[value];
            print(fval);
	    break;
        default:
            print('???');
	}
    }
    else {
        string typechar = substr(regtypename, type & PARROT_ARG_TYPE_MASK, 1);
        print(typechar, value);
    }
}

function dump_bytecode(var seg, var const_table)
{
    int n = elements(seg);
    say("Bytecode:\n\tLength: ", n);
    var oplib = new 'OpLib';
    for (int offset= 0; offset < n; ++offset) {
        print("\t", offset, ': ');
        int code = seg[offset];
        print(code, " - ");
        var opcode = oplib[code];
	int nargs = elements(opcode);
        string opstr = opcode;
        print(opstr);

        // Special opcodes
        if (opstr == 'set_args_pc' || opstr == 'set_returns_pc' ||
                opstr == 'get_results_pc' || opstr == 'get_params_pc')
        {
            ++offset;
            int sigindex = seg[offset];
            var sig = const_table[sigindex];
            int siglen = sig;
            print(' PC', sigindex, '{', siglen, '}');
            string sep = ' ';
            for (int a = 0; a < siglen; ++a) {
                ++offset;
                int sigvalue = sig[a];
                int value = seg[offset];
                print(sep);
                sep = ', ';
                showarg(sigvalue, value, const_table);
            }
        }
        else { // Not so special
            string sep = ' ';
            for (int arg = 0; arg < nargs; ++arg) {
                ++offset;
                print(sep);
                sep = ', ';
                int value = seg[offset];
		int type = opcode[arg];
                showarg(type, value, const_table);
	    }
        }
        say();
    }
}

function main(argv)
{
    var file = new 'FileHandle'.open(argv[1], 'rb');
    string content= file.readall();
    file.close();

    var pack= new 'Packfile';
    pack =: content;
    say('Header:');
    for (string key in [
            'wordsize',
            'byteorder',
            'fptype',
            'version_major',
            'version_minor',
            'version_patch',
            'bytecode_major',
            'bytecode_minor',
            'uuid_type'
            ] ) {
        int n= pack[key];
        say("\t", key, ': ', n);
    }
    say('Directory:');
    var dir= pack.get_directory();
    var const_table = null;
    for (var it in dir) {
        print("\tSegment: ", it, ' type ');
        var seg = it.value();
        int type = seg.type();
        switch (type)
        {
        case PF_FIXUP_SEG:       print("FIXUP"); break;
        case PF_CONST_SEG:       print("CONST");
            const_table = seg;
            break;
        case PF_BYTEC_SEG:       print("BYTEC"); break;
        case PF_DEBUG_SEG:       print("DEBUG"); break;
        case PF_ANNOTATIONS_SEG: print("ANNOTATIONS"); break;
        default:                 print("Unknown");
        }
        say(' (', type, ')');
    }
    for (var it in dir) {
        var seg = it.value();
        int type = seg.type();
        switch (type)
        {
        case PF_CONST_SEG:
            dump_constants(seg);
            break;
        case PF_FIXUP_SEG:
            dump_fixup(seg, const_table);
            break;
        case PF_ANNOTATIONS_SEG:
            dump_annotations(seg);
            break;
        case PF_BYTEC_SEG:
            dump_bytecode(seg, const_table);
            break;
        default:
            break;
        }
    }
}

// That's all folks!
