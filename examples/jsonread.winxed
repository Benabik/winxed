#! winxed

function read_data(string filename)
{
    var json = load_language('data_json');
    var file = open(filename);
    file.encoding('utf8');
    var code = json.compile(file.readall());
    var data = code();
    return data;
}

function dump(object, int indent[optional])
{
    switch {
      case object instanceof ['ResizablePMCArray']:
        say('  ' * indent, 'Array[');
        int n = elements(object);
        for (int i = 0; i < n; ++i) {
            say('  ' * indent, i);
            dump(object[i], indent + 1);
        }
        say('  ' * indent, ']');
        break;
      case object instanceof ['Hash']:
        say('  ' * indent, 'Object{');
        for (string key in object) {
            say('  ' * indent, key, ' :');
            dump(object[key], indent + 1);
        }
        say('  ' * indent, '}');
        break;
      default:
        say('  ' * indent, object);
    }
}

function main(argv)
{
    var data;
    try {
        data= read_data(argv[1]);
    }
    catch (e)
    {
       say('Error reading data: ', e['message']);
       return;
    }
    dump(data);
}

// End
