#! winxed
// ajax.winxed

// A simplified implementation of the W3C XMLHttpRequest specification.
// See: http://www.w3.org/TR/XMLHttpRequest/

//**********************************************************************

class XMLHttpRequest
{
    // Private
    // Internal usage variables and methods

    var ua;  // Parrot LWP;UserAgent
    var url; // URL specifid in the open method
    var async;
    // Constants for readyState values
    const int UNSENT           = 0;
    const int OPENED           = 1;
    const int HEADERS_RECEIVED = 2;
    const int LOADING          = 3;
    const int DONE             = 4;

    function init [vtable]()
    {
        self.readyState = UNSENT;
        self.status = 0;
        self.responseText = '';
        using extern LWP.UserAgent;
    }

    function changeState(int state)
    {
        self.readyState=: state;
        var onreadystatechange = self.onreadystatechange;
        if (onreadystatechange != null)
            onreadystatechange();
    }

    // Public
    // Properties and methods defined by the specification.

    var readyState;
    var status;
    var responseText;
    var onreadystatechange;

    function open(string method, string url, int async)
    {
        self.async = async;
        string m = upcase(method);
        if (m != 'GET')
            throw Error('Unsupported method');

        self.url = url;
        self.changeState(OPENED);
    }
    function send(var data)
    {
        if (self.readyState != OPENED)
            throw Error('INVALID_STATE_ERR');
        string url = self.url;

        if (self.async)
            self.changeState(OPENED);

        self.ua = new LWP.UserAgent();
        var resp = self.ua.get(url,
           {
               'Connection' : 'close'
           }:[named,flat]
        );
        if (self.async)
            self.changeState(HEADERS_RECEIVED);
        if (self.async)
            self.changeState(LOADING);
        self.status =: resp.code();
        self.responseText = resp.content();
        self.changeState(DONE);
    }
}

//**********************************************************************

// main function for testing.

class StChange
{
    var req;

    function invoke[vtable]()
    {
        var req = self.req;
        say('Status changed to ', req.readyState);
        if (req.readyState == 4) {
            say('Status: ', req.status);
            self.showresp();
        }
    }

    function showresp()
    {
        var req = self.req;
        say("Response:\n", req.responseText);
    }
}

function main(var args)
{
    int nargs = elements(args);
    if (nargs < 2)
        Error('No URL');

    int use_async = 1;

    var req = new XMLHttpRequest();
    var stchange = new StChange();
    stchange.req = req;

    if (use_async)
        req.onreadystatechange = stchange;

    req.open('get', args[1], use_async);
    req.send(null);
    if (! use_async)
        stchange.showresp();
}

// End
