#! winxed

// Winxed compiler driver.

$load 'Getopt/Obj.pbc';

class Options : Getopt.Obj
{
    var name;
    var options;
    var opts;

    function Options(argv, int unused)
    {
        var options = [
          [ 'c',        'Compile only. Same as --target=pir' ],
          [ 'e=s',      'Evaluate' ],
          [ 'help',     'Show this help' ]
        ];
        self.options = options;
        for (var o in options)
            self.push_string(o[0]);
        self.notOptStop(1);
        self.name = argv.shift();
        self.opts = self.get_options(argv);
    }
    function get()
    {
        return self.opts;
    }
    function showhelp()
    {
        say('Usage: ', self.name, ' [options] [program] [args]');
        say('  Available options:');
        int l= 0;
        int i;
        var o;
        for (o in self.options) {
            i= length(o[0]) + 4;
            if (i > l) l= i;
        }
        for (o in self.options) {
            string s= o[0];
            if (length(s) > 1 && substr(s, 1, 1) != '=')
                s= '--' + s;
            else
                s= '-' + s;
            i= l - length(s);
            say('    ', s, ' ' * i, '->  ', o[1]);
        }
    }
}


function extname(string filename, string ext)
{
    // Strip a possible .winxed extension from filename,
    // add ext, and return the result.
    string newname;
    int l = length(filename);
    if (l > 7 && substr(filename, -7) == '.winxed')
        newname = substr(filename, 0, l - 7) + ext;
    else
        newname = filename + ext;
    return newname;
}

function main [main] (argv)
{
    var optionset = new Options(argv, 0);
    var options = optionset.get();

    var help        = options['help'];
    var compileonly = options['c'];
    var eval        = options['e'];

    if (help != null) {
        optionset.showhelp();
        return;
    }

    var compiler = load_language('winxed');
    if (compiler == null)
        die("winxed: Cannot load language");

    var code;

    if (eval == null) {
        if (elements(argv) < 1) {
            say("ERROR: No program specified");
            optionset.showhelp();
            return 1;
        }
        string srcfile = argv[0];
        if (compileonly != null) {
            var handleout = new 'StringHandle';
            handleout.open('', 'w');
            compiler.compile_from_file_to_pir(srcfile, handleout);
            handleout.close();
            string pircode = handleout.read(0);
            string pirfile = extname(srcfile, '.pir');
            var outfile = open(pirfile, 'w');
            outfile.print(pircode);
            outfile.close();
            return 0;
        }
        else
            code = compiler.compile_from_file(srcfile);
    }
    else {
        if (compileonly != null)
            die('option -c with -e not supported yet');
        string expr = 'function main[main](argv){' + string(eval) + ';}';
        code = compiler.compile(expr);
        argv.unshift('__EVAL__');
    }

    // Simplified progrm start: look for a sub called 'main'
    var sub;
    for (int i = 0; ; ++i) {
        sub = code[i];
        if (sub == null) break;
        if (string(sub) == 'main') break;
    }

    try {
        sub(argv);
    }
    catch (e) {
        string msg = e['message'];
        cry(e);
        return 1;
    }
}

// End
