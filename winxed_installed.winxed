#! winxed

// Winxed compiler driver.

const int DEBUG = 0;

$include_const 'iglobals.pasm';
$include_const 'libpaths.pasm';
$include_const 'except_types.pasm';

$load 'Getopt/Obj.pbc';

class WinxedDriverOptions : [ 'Getopt', 'Obj']
{
    var name;
    var options;
    var opts;

    function WinxedDriverOptions(argv)
    {
        var options = [
          [ 'c',        'Compile to pir' ],
          [ 'e=s',      'Evaluate' ],
          [ 'o=s',      'Object name' ],
          [ 'target=s', 'Set target type' ],
          [ 'L=s',      'Add to parrot library search path' ],
          [ 'I=s',      'Add to parrot include search path' ],
          [ 'nowarn',   'No warnings' ],
          [ 'noan',     'No code annotations' ],
          [ 'help',     'Show this help' ]
        ];
        self.options = options;
        for (var o in options)
            self.push_string(o[0]);
        self.notOptStop(1);
        self.name = argv.shift();
        self.opts = self.get_options(argv);
    }
    function getbool(string option)
    {
        var value = self.opts[option];
        return value != null;
    }
    function getstring(string option)
    {
        var value = self.opts[option];
        string result = null;
        if (value != null)
            result = value;
        return result;
    }
    function showhelp()
    {
        say('Usage: ', self.name, ' [options] [program] [args]');
        say('  Available options:');
        int l= 0;
        int i;
        var o;
        for (o in self.options) {
            i= length(o[0]) + 4;
            if (i > l) l= i;
        }
        for (o in self.options) {
            string s= o[0];
            if (length(s) > 1 && substr(s, 1, 1) != '=')
                s= '--' + s;
            else
                s= '-' + s;
            i= l - length(s);
            say('    ', s, ' ' * i, '->  ', o[1]);
        }
    }
}


function extname(string filename, string ext)
{
    // Strip a possible .winxed extension from filename,
    // add ext, and return the result.
    string newname;
    int l = length(filename);
    if (l > 7 && substr(filename, -7) == '.winxed')
        newname = substr(filename, 0, l - 7) + ext;
    else
        newname = filename + ext;
    return newname;
}

function main [main] (argv)
{
    var optionset = new WinxedDriverOptions(argv);

    int help          = optionset.getbool('help');
    int compileonly   = optionset.getbool('c');
    string target     = optionset.getstring('target');
    string eval       = optionset.getstring('e');
    string objectname = optionset.getstring('o');
    string opt_L      = optionset.getstring('L');
    string opt_I      = optionset.getstring('I');
    int nowarn        = optionset.getbool('nowarn');
    int noan          = optionset.getbool('noan');

    if (help) {
        optionset.showhelp();
        exit(0);
    }

    if (opt_L != null) {
        var interp;
        ${ getinterp interp };
        var lpaths = interp[IGLOBALS_LIB_PATHS];
        var pathlib = lpaths[PARROT_LIB_PATH_LIBRARY];
        pathlib.push(string(opt_L));
    }
    if (opt_I != null) {
        var interp;
        ${ getinterp interp };
        var lpaths = interp[IGLOBALS_LIB_PATHS];
        var pathlib = lpaths[PARROT_LIB_PATH_INCLUDE];
        pathlib.push(string(opt_I));
    }

    var compileoptions = { "noan" : noan, "nowarn" : nowarn };
    if (compileonly) {
        if (target != null)
            die("options -c and --target can't be used together");
        compileoptions["target"] = "pir";
    }
    else {
        if (target == null)
            target = '';
        switch (target) {
          case '':
          case 'run':
            break;
          case 'pir':
            compileonly = true;
            compileoptions["target"] = target;
            break;
          default:
            die("Invalid target '" + target + "'");
        }
    }

    if (objectname != null && ! compileonly)
        die('-o without -c or --target is not supported yet');

    var compiler;
    try 
        compiler = load_language('winxed');
    catch () { }

    if (compiler == null) {
        if (DEBUG) {
            load_bytecode("winxedst2.pbc");
            compiler = compreg('winxed');
            if (compiler == null)
                die("winxed: Cannot load language or stage");
        }
        else
            die("winxed: Cannot load language");
    }

    var code;
    string outfilename = null;
    if (eval == null) {
        if (elements(argv) < 1) {
            say("ERROR: No program specified");
            optionset.showhelp();
            exit(1);
        }
        string srcfilename = argv[0];
        if (compileonly && objectname == null)
            outfilename = extname(srcfilename, '.pir');
        code = compiler.compile_from_file(srcfilename,
                compileoptions:[named, flat]);
    }
    else {
        string expr = 'function main[main](argv){' + eval + ';}';
        code = compiler.compile(expr,
                compileoptions:[named, flat]);
        if (! compileonly)
            argv.unshift('__EVAL__');
    }

    if (compileonly) {
        if (outfilename == null)
            outfilename = objectname;
        int create = outfilename != null && outfilename != "-";
        var outfile = create ? open(outfilename, 'w') : getstdout();
        outfile.print(code);
        if (create)
            outfile.close();
        exit(0);
    }

    // Simplified progrm start: look for a sub called 'main'
    var sub;
    for (int i = 0; ; ++i) {
        sub = code[i];
        if (sub == null) break;
        if (string(sub) == 'main') break;
    }

    int retval = 0;
    try [handle_types_except(CONTROL_EXIT)] {
        var retvalp = sub(argv);
        if (retvalp != null)
            retval = retvalp;
    }
    catch (e) {
        string msg = e['message'];
        if (msg != null && msg != '')
            cry(msg);
        retval = 1;
    }
    exit(retval);
}

// End
